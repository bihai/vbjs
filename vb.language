start
    = module EOF
    / expression EOF

module = _ options func_defs

options = option*
option = OPTION [^\r\n]* EOL

func_defs = func_def*
func_def = FUNCTION uname args_spec statements END FUNCTION
args_spec = LEFT_PAREN (uname (comma uname)* )? RIGHT_PAREN type_spec? on_error?
statements = statement*
type_spec = AS uname
on_error = ONERROR GOTO uname

statement
    = resume_statement
    / exit_statement
    / dim_statement
    / set_statement
    / if_statement
    / assign_statement
    / label_statement
    / call_statement
resume_statement = RESUME uname
exit_statement = EXIT FUNCTION
dim_statement = DIM uname AS uname
set_statement = (CONST / SET) uname EQUAL expression
if_statement = IF if_condition THEN statements ENDIF
assign_statement = uname EQUAL expression
label_statement = uname COLON
call_statement = callee call_args?
callee = uname (identifier_op uname)*
call_args = LEFT_PAREN (expression (comma expression)* )? RIGHT_PAREN

if_condition
    = expression EQUAL expression
    / expression

expression
    = expression_in_braces
    / concat_expr

expression_in_braces = LEFT_PAREN expression RIGHT_PAREN

concat_expr
    = add_expr (concat_op add_expr)*

add_expr
    = call_expr (plus_op call_expr)*

call_expr
    = lazy_call_expr
    / name LEFT_PAREN (expression (comma expression)* )? RIGHT_PAREN
    / value

lazy_call_expr
    = lazy_name LEFT_PAREN (lazy_value (comma lazy_value)* )? RIGHT_PAREN

lazy_value
    = bracketed_identifier
    / literal
    
value
    = identifier_expr
    / literal
    / TRUE
    / FALSE
    / number

identifier_expr
    = identifier (identifier_op identifier)*

_ = ( [ \t\r\n] / comment )*
__ = [ \t]*

comment = "'" [^\r\n]*

identifier
    = bracketed_identifier
    / uname
bracketed_identifier = '[' name_in_brackets ']' __
name_in_brackets = [A-Za-z_][A-Za-z0-9_ ]*
name = [A-Za-z_][A-Za-z0-9_]* _

lazy_name = 'Sum'
literal = '"' literal_text '"' __
literal_text = [^"]*
concat_op = '&' __
plus_op = '+' __
comma = ',' __
identifier_op = '!' / '.'
number = [0-9]+ _

uname = !(keyword ![A-Za-z0-9_]) uname_itself _
uname_itself = [A-Za-z_][A-Za-z0-9_]*
FUNCTION = 'Function' _
END = 'End' __
OPTION = 'Option' __
ONERROR = 'On' [ \t]+ 'Error' __
GOTO = 'GoTo' __
RESUME = 'Resume' __
EXIT = 'Exit' __
DIM = 'Dim' __
CONST = 'Const' __
SET = 'Set' __
IF = 'If' __
THEN = 'Then' _
ENDIF = END IF _
EQUAL = '=' __
COLON = ':' _
TRUE = 'True' _
FALSE = 'False' _
LEFT_PAREN = '(' _
RIGHT_PAREN = ')' _
keyword
    = 'End'
    / 'Function'
    / 'Then'
    / 'If'
    / 'Set'
    / 'Const'
AS = 'As' __

EOF = !.
EOL = [\r\n]+ / EOF
